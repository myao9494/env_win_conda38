diff --git a/CMakeLists.txt b/CMakeLists.txt
index 0b4c1a33d..21817d3de 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -284,7 +284,6 @@ if(NOT PCL_SHARED_LIBS OR ((WIN32 AND NOT MINGW) AND NOT PCL_BUILD_WITH_FLANN_DY
   set(FLANN_USE_STATIC ON)
 endif()
 find_package(FLANN 1.7.0 REQUIRED)
-include_directories(${FLANN_INCLUDE_DIRS})

 # libusb-1.0
 option(WITH_LIBUSB "Build USB RGBD-Camera drivers" TRUE)
diff --git a/PCLConfig.cmake.in b/PCLConfig.cmake.in
index 6fadb5954..2099698a9 100644
--- a/PCLConfig.cmake.in
+++ b/PCLConfig.cmake.in
@@ -651,7 +651,10 @@ endif(NOT "${PCL_DEFINITIONS}" STREQUAL "")
 pcl_remove_duplicate_libraries(PCL_COMPONENTS PCL_LIBRARIES)

 # Add 3rd party libraries, as user code might include our .HPP implementations
-list(APPEND PCL_LIBRARIES ${BOOST_LIBRARIES} ${QHULL_LIBRARIES} ${OPENNI_LIBRARIES} ${OPENNI2_LIBRARIES} ${ENSENSO_LIBRARIES} ${davidSDK_LIBRARIES} ${DSSDK_LIBRARIES} ${RSSDK_LIBRARIES} ${FLANN_LIBRARIES} ${VTK_LIBRARIES})
+list(APPEND PCL_LIBRARIES ${BOOST_LIBRARIES} ${QHULL_LIBRARIES} ${OPENNI_LIBRARIES} ${OPENNI2_LIBRARIES} ${ENSENSO_LIBRARIES} ${davidSDK_LIBRARIES} ${DSSDK_LIBRARIES} ${RSSDK_LIBRARIES} ${VTK_LIBRARIES})
+if (TARGET FLANN::FLANN)
+  list(APPEND PCL_LIBRARIES FLANN::FLANN)
+endif()

 find_package_handle_standard_args(PCL DEFAULT_MSG PCL_LIBRARIES PCL_INCLUDE_DIRS)
 mark_as_advanced(PCL_LIBRARIES PCL_INCLUDE_DIRS PCL_LIBRARY_DIRS)
diff --git a/cmake/Modules/FindFLANN.cmake b/cmake/Modules/FindFLANN.cmake
index 1cecacfa9..34ff22430 100644
--- a/cmake/Modules/FindFLANN.cmake
+++ b/cmake/Modules/FindFLANN.cmake
@@ -1,63 +1,145 @@
-###############################################################################
-# Find FLANN
+#.rst:
+# FindFLANN
+# --------
+#
+# Try to find FLANN library and include files.
+#
+# IMPORTED Targets
+# ^^^^^^^^^^^^^^^^
+#
+# This module defines the :prop_tgt:`IMPORTED` targets:
+#
+# ``FLANN::FLANN``
+#  Defined if the system has FLANN.
+#
+# Result Variables
+# ^^^^^^^^^^^^^^^^
+#
+# This module sets the following variables:
+#
+# ::
+#
+#   FLANN_FOUND               True in case FLANN is found, otherwise false
+#   FLANN_DEFINITIONS         Compiler flags for FLANN.
+#   FLANN_INCLUDE_DIR         Location of FLANN header files
+#   FLANN_INCLUDE_DIRS        Location of FLANN header files (including dependencies)
+#   FLANN_LIBRARY             FLANN release library
+#   FLANN_LIBRARY_DEBUG       FLANN debug library
+#   FLANN_LIBRARIES           FLANN release and debug library
+#
+# Example usage
+# ^^^^^^^^^^^^^
+#
+# ::
+#
+#     find_package(FLANN REQUIRED)
+#
+#     add_executable(foo foo.cc)
+#     target_link_libraries(foo FLANN::FLANN)
 #
-# This sets the following variables:
-# FLANN_FOUND - True if FLANN was found.
-# FLANN_INCLUDE_DIRS - Directories containing the FLANN include files.
-# FLANN_LIBRARIES - Libraries needed to use FLANN.
-# FLANN_DEFINITIONS - Compiler flags for FLANN.
-# If FLANN_USE_STATIC is specified and then look for static libraries ONLY else
-# look for shared ones
+
+find_package(PkgConfig QUIET)
+if(FLANN_FIND_VERSION)
+  pkg_check_modules(PC_FLANN flann>=${FLANN_FIND_VERSION})
+else()
+  pkg_check_modules(PC_FLANN flann)
+endif()
+
+set(FLANN_DEFINITIONS ${PC_FLANN_CFLAGS_OTHER})

 if(FLANN_USE_STATIC)
   set(FLANN_RELEASE_NAME flann_cpp_s)
   set(FLANN_DEBUG_NAME flann_cpp_s-gd)
-else(FLANN_USE_STATIC)
+  set(FLANN_DEFINITIONS ${FLANN_DEFINITIONS} "-DFLANN_STATIC")
+else()
   set(FLANN_RELEASE_NAME flann_cpp)
   set(FLANN_DEBUG_NAME flann_cpp-gd)
-endif(FLANN_USE_STATIC)
+endif()

-find_package(PkgConfig QUIET)
-if (FLANN_FIND_VERSION)
-    pkg_check_modules(FLANN flann>=${FLANN_FIND_VERSION})
-else(FLANN_FIND_VERSION)
-    pkg_check_modules(FLANN flann)
-endif(FLANN_FIND_VERSION)
-
-if(NOT FLANN_FOUND)
-    find_path(FLANN_INCLUDE_DIR flann/flann.hpp
-              HINTS "${FLANN_ROOT}" "$ENV{FLANN_ROOT}"
-              PATHS "$ENV{PROGRAMFILES}/flann" "$ENV{PROGRAMW6432}/flann"
-              PATH_SUFFIXES include)
-
-    find_library(FLANN_LIBRARY
-                 NAMES ${FLANN_RELEASE_NAME}
-                 HINTS "${FLANN_ROOT}" "$ENV{FLANN_ROOT}"
-                 PATHS "$ENV{PROGRAMFILES}/flann" "$ENV{PROGRAMW6432}/flann"
-                 PATH_SUFFIXES lib)
-
-    find_library(FLANN_LIBRARY_DEBUG
-                 NAMES ${FLANN_DEBUG_NAME} ${FLANN_RELEASE_NAME}
-                 HINTS "${FLANN_ROOT}" "$ENV{FLANN_ROOT}"
-                 PATHS "$ENV{PROGRAMFILES}/flann" "$ENV{PROGRAMW6432}/flann"
-                 PATH_SUFFIXES lib debug/lib)
-
-    if(NOT FLANN_LIBRARY_DEBUG)
-      set(FLANN_LIBRARY_DEBUG ${FLANN_LIBRARY})
-    endif(NOT FLANN_LIBRARY_DEBUG)
-
-    set(FLANN_INCLUDE_DIRS ${FLANN_INCLUDE_DIR})
-    set(FLANN_LIBRARIES optimized ${FLANN_LIBRARY} debug ${FLANN_LIBRARY_DEBUG})
-
-    include(FindPackageHandleStandardArgs)
-    find_package_handle_standard_args(FLANN DEFAULT_MSG FLANN_LIBRARY FLANN_INCLUDE_DIR)
-
-    mark_as_advanced(FLANN_LIBRARY FLANN_LIBRARY_DEBUG FLANN_INCLUDE_DIR)
+find_path(FLANN_INCLUDE_DIR
+  NAMES
+    flann/flann.hpp
+  HINTS
+    ${PC_FLANN_INCLUDE_DIRS}
+    ${FLANN_ROOT}
+    $ENV{FLANN_ROOT}
+  PATHS
+    $ENV{PROGRAMFILES}/Flann
+    $ENV{PROGRAMW6432}/Flann
+  PATH_SUFFIXES
+    include
+)
+
+find_library(FLANN_LIBRARY
+  NAMES
+    ${FLANN_RELEASE_NAME}
+  HINTS
+    ${PC_FLANN_LIBRARY_DIRS}
+    ${FLANN_ROOT}
+    $ENV{FLANN_ROOT}
+  PATHS
+    $ENV{PROGRAMFILES}/Flann
+    $ENV{PROGRAMW6432}/Flann
+  PATH_SUFFIXES
+    lib
+)
+
+find_library(FLANN_LIBRARY_DEBUG
+  NAMES
+    ${FLANN_DEBUG_NAME}
+  HINTS
+    ${PC_FLANN_LIBRARY_DIRS}
+    ${FLANN_ROOT}
+    $ENV{FLANN_ROOT}
+  PATHS
+    $ENV{PROGRAMFILES}/Flann
+    $ENV{PROGRAMW6432}/Flann
+  PATH_SUFFIXES
+    lib
+)
+
+if(FLANN_LIBRARY AND FLANN_LIBRARY_DEBUG)
+  set(FLANN_LIBRARIES optimized ${FLANN_LIBRARY} debug ${FLANN_LIBRARY_DEBUG})
+else()
+  set(FLANN_LIBRARIES ${FLANN_LIBRARY})
 endif()

+set(FLANN_INCLUDE_DIRS
+  ${FLANN_INCLUDE_DIR}
+)
+
+include(FindPackageHandleStandardArgs)
+find_package_handle_standard_args(
+  FLANN DEFAULT_MSG
+  FLANN_LIBRARIES FLANN_INCLUDE_DIR
+)
+
 if(FLANN_FOUND)
-  message(STATUS "FLANN found (include: ${FLANN_INCLUDE_DIRS}, lib: ${FLANN_LIBRARIES})")
-  if(FLANN_USE_STATIC)
-    add_definitions(-DFLANN_STATIC)
-  endif(FLANN_USE_STATIC)
-endif(FLANN_FOUND)
+  if(NOT TARGET FLANN::FLANN)
+    if (FLANN_USE_STATIC)
+      add_library(FLANN::FLANN STATIC IMPORTED)
+    else()
+      add_library(FLANN::FLANN SHARED IMPORTED)
+    endif()
+    set_target_properties(FLANN::FLANN PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${FLANN_INCLUDE_DIRS}")
+    set_target_properties(FLANN::FLANN PROPERTIES INTERFACE_COMPILE_DEFINITIONS "${FLANN_DEFINITIONS}")
+    if(FLANN_LIBRARY)
+      set_property(TARGET FLANN::FLANN APPEND PROPERTY IMPORTED_CONFIGURATIONS "RELEASE")
+      set_target_properties(FLANN::FLANN PROPERTIES IMPORTED_LINK_INTERFACE_LANGUAGES_RELEASE "CXX")
+      if(WIN32)
+        set_target_properties(FLANN::FLANN PROPERTIES IMPORTED_IMPLIB_RELEASE "${FLANN_LIBRARY}")
+      else()
+        set_target_properties(FLANN::FLANN PROPERTIES IMPORTED_LOCATION_RELEASE "${FLANN_LIBRARY}")
+      endif()
+    endif()
+    if(FLANN_LIBRARY_DEBUG)
+      set_property(TARGET FLANN::FLANN APPEND PROPERTY IMPORTED_CONFIGURATIONS "DEBUG")
+      set_target_properties(FLANN::FLANN PROPERTIES IMPORTED_LINK_INTERFACE_LANGUAGES_DEBUG "CXX")
+      if(WIN32)
+        set_target_properties(FLANN::FLANN PROPERTIES IMPORTED_IMPLIB_DEBUG "${FLANN_LIBRARY_DEBUG}")
+      else()
+        set_target_properties(FLANN::FLANN PROPERTIES IMPORTED_LOCATION_DEBUG "${FLANN_LIBRARY_DEBUG}")
+      endif()
+    endif()
+  endif()
+endif()
diff --git a/doc/tutorials/content/sources/vfh_recognition/CMakeLists.txt b/doc/tutorials/content/sources/vfh_recognition/CMakeLists.txt
index 9693792f1..525da3445 100644
--- a/doc/tutorials/content/sources/vfh_recognition/CMakeLists.txt
+++ b/doc/tutorials/content/sources/vfh_recognition/CMakeLists.txt
@@ -1,8 +1,5 @@
 cmake_minimum_required(VERSION 2.8 FATAL_ERROR)

-# we need FindFLANN.cmake
-list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR})
-
 project(vfh_cluster_classifier)

 find_package(PCL 1.2 REQUIRED)
@@ -14,16 +11,14 @@ find_package(HDF5)
 if(HDF5_FOUND)

   find_package(FLANN)
-  include_directories(${FLANN_INCLUDE_DIRS})
-
   include_directories(${HDF5_INCLUDE_DIR})

   add_executable(build_tree build_tree.cpp)
   target_link_libraries(build_tree ${PCL_LIBRARIES} ${Boost_LIBRARIES}
-                                 ${FLANN_LIBRARIES} ${HDF5_hdf5_LIBRARY})
+                                 FLANN::FLANN ${HDF5_hdf5_LIBRARY})

   add_executable(nearest_neighbors nearest_neighbors.cpp)
   target_link_libraries(nearest_neighbors ${PCL_LIBRARIES}
-                                        ${Boost_LIBRARIES} ${FLANN_LIBRARIES} ${HDF5_hdf5_LIBRARY}
+                                        ${Boost_LIBRARIES} FLANN::FLANN ${HDF5_hdf5_LIBRARY}
                                         )
 endif(HDF5_FOUND)
diff --git a/kdtree/CMakeLists.txt b/kdtree/CMakeLists.txt
index 311dffc13..aeed14dee 100644
--- a/kdtree/CMakeLists.txt
+++ b/kdtree/CMakeLists.txt
@@ -28,7 +28,7 @@ if(build)
     set(LIB_NAME "pcl_${SUBSYS_NAME}")
     include_directories("${CMAKE_CURRENT_SOURCE_DIR}/include")
     PCL_ADD_LIBRARY("${LIB_NAME}" "${SUBSYS_NAME}" ${srcs} ${incs} ${impl_incs})
-    target_link_libraries("${LIB_NAME}" pcl_common ${FLANN_LIBRARIES})
+    target_link_libraries("${LIB_NAME}" pcl_common FLANN::FLANN)
     set(EXT_DEPS flann)
     PCL_MAKE_PKGCONFIG("${LIB_NAME}" "${SUBSYS_NAME}" "${SUBSYS_DESC}"
                        "${SUBSYS_DEPS}" "${EXT_DEPS}" "" "" "")
diff --git a/search/CMakeLists.txt b/search/CMakeLists.txt
index cd82a0ef5..432465c1b 100644
--- a/search/CMakeLists.txt
+++ b/search/CMakeLists.txt
@@ -38,7 +38,7 @@ if(build)
     set(LIB_NAME "pcl_${SUBSYS_NAME}")
     include_directories("${CMAKE_CURRENT_SOURCE_DIR}/include")
     PCL_ADD_LIBRARY("${LIB_NAME}" "${SUBSYS_NAME}" ${srcs} ${incs} ${impl_incs})
-    target_link_libraries("${LIB_NAME}" pcl_common ${FLANN_LIBRARIES} pcl_octree pcl_kdtree)
+    target_link_libraries("${LIB_NAME}" pcl_common FLANN::FLANN pcl_octree pcl_kdtree)
     list(APPEND EXT_DEPS flann)
     PCL_MAKE_PKGCONFIG("${LIB_NAME}" "${SUBSYS_NAME}" "${SUBSYS_DESC}" "${SUBSYS_DEPS}" "" "" "" "")

diff --git a/test/features/CMakeLists.txt b/test/features/CMakeLists.txt
index cedb0cb42..6745936a2 100644
--- a/test/features/CMakeLists.txt
+++ b/test/features/CMakeLists.txt
@@ -88,7 +88,7 @@ if (build)
                  ARGUMENTS "${PCL_SOURCE_DIR}/test/bun0.pcd")
     PCL_ADD_TEST(features_narf test_narf
                  FILES test_narf.cpp
-                 LINK_WITH pcl_gtest pcl_features ${FLANN_LIBRARIES})
+                 LINK_WITH pcl_gtest pcl_features FLANN::FLANN)
     PCL_ADD_TEST(a_ii_normals_test test_ii_normals
                  FILES test_ii_normals.cpp
                  LINK_WITH pcl_gtest pcl_io pcl_features
